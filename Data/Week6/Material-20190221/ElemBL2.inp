TITLE: Validating Hypothesized Causal Structure for Calibration Group
       Modified Baseline model (Byrne 2012, p. 268)
       Combined F2 and F3, re-numbered the factors

DATA: FILE IS "elemind1.dat";
      FORMAT IS (19F4.2/13F4.2);

VARIABLE:
       NAMES ARE ROLEA1 ROLEA2 ROLEC1 ROLEC2 WORK1 WORK2
       CCLIM1 CCLIM2 CCLIM3 CCLIM4 DEC1 DEC2 SSUP1 SSUP2
       PSUP1 PSUP2 SELF1 SELF2 SELF3 ELC1 ELC2 ELC3 ELC4 ELC5
       EE1 EE2 EE3 DP1 DP2 PA1 PA2 PA3;
USEVARIABLES ARE ROLEA1 - PA3;

ANALYSIS: TYPE = GENERAL;
          ESTIMATOR IS MLM;

MODEL:
  F1 BY ROLEA1-ROLEA2;
  !F2 BY ROLEC1-ROLEC2;
  !F3 BY WORK1-WORK2;
  F2 BY ROLEC1-WORK2;
  ! numbers 2-12 decreased to 1-11:
  F3 BY CCLIM1-CCLIM4;
  F4 BY DEC1-DEC2;
  F5 BY SSUP1-SSUP2;
  F6 BY PSUP1-PSUP2;
  F7 BY SELF1-SELF3;
  F8 BY ELC1-ELC5;
  F9 BY EE1-EE3;
  F10 BY DP1-DP2;
  F11 BY PA1-PA3;

  ! also here...
  F7 ON F4 F5 F6;
  F8 ON F4;
  F9 ON F2 F3;
  F10 ON F2 F9;
  F11 ON F1 F7 F8 F9 F10;

OUTPUT: MODINDICES STDYX;

